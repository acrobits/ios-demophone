#import "CallActionHelpers.h"

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
using ali::operator""_s;
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
@interface CallActionHelpers ()
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*

@property (nonatomic, strong) NSHashTable<id<TransferChangedDelegate>> *transferChangedDelegates;
@property (nonatomic, strong) NSHashTable<id<AttendedTransferChangedDelegate>> *attendedTransferChangedDelegates;
@property (nonatomic, strong) NSHashTable<id<ForwardChangedDelegate>> *forwardChangedDelegates;

@end

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
@implementation CallActionHelpers
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
+ (instancetype)sharedInstance
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    static CallActionHelpers *sharedInstance = nil;
    static dispatch_once_t onceToken;
    dispatch_once(&onceToken, ^{
        sharedInstance = [[self alloc] init];
    });
    return sharedInstance;
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
- (instancetype)init
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    self = [super init];
    if (self) {
        _transferChangedDelegates = [NSHashTable weakObjectsHashTable];
        _attendedTransferChangedDelegates = [NSHashTable weakObjectsHashTable];
        _forwardChangedDelegates = [NSHashTable weakObjectsHashTable];
    }
    return self;
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(void)addTransferChangedDelegate:(id<TransferChangedDelegate>)delegate
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    [_transferChangedDelegates addObject:delegate];
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(void)removeTransferChangedDelegate:(id<TransferChangedDelegate>)delegate
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    [_transferChangedDelegates removeObject:delegate];
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(void)addAttendedTransferChangedDelegate:(id<AttendedTransferChangedDelegate>)delegate
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    [_attendedTransferChangedDelegates addObject:delegate];
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(void)removeAttendedTransferChangedDelegate:(id<AttendedTransferChangedDelegate>)delegate
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    [_attendedTransferChangedDelegates removeObject:delegate];
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(void)addForwardChangedDelegate:(id<ForwardChangedDelegate>)delegate
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    [_forwardChangedDelegates addObject:delegate];
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(void)removeForwardChangedDelegate:(id<ForwardChangedDelegate>)delegate
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    [_forwardChangedDelegates removeObject:delegate];
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(void)beginTransfer:(Softphone::EventHistory::CallEvent::Pointer)call
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    if (call.is_null())
        return;
    
    call->transients["markedForTransfer"_s] = "1"_s;
    
    if (Softphone::instance()->calls()->isHeld(call).local != Call::HoldState::Held)
        Softphone::instance()->calls()->setHeld(call, true);
    
    [self notifyTransferStarted:call];
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(void)cancelTransfer:(Softphone::EventHistory::CallEvent::Pointer)call
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    if (call.is_null())
        return;
    
    call->transients.erase("markedForTransfer"_s);
    
    if (Softphone::instance()->calls()->isHeld(call).local == Call::HoldState::Held)
        Softphone::instance()->calls()->setHeld(call, false);
    
    [self notifyTransferCancelled:call];
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(BOOL)isTransferring:(Softphone::EventHistory::CallEvent::Pointer)call
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    if (call.is_null())
        return false;
    
    return call->transients["markedForTransfer"_s] == "1"_s;
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(void)beginAttendedTransfer:(Softphone::EventHistory::CallEvent::Pointer)call
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    if (call.is_null())
        return;
    
    call->transients["markedForAttendedTransfer"_s] = "1"_s;
    
    if (Softphone::instance()->calls()->isHeld(call).local != Call::HoldState::Held)
        Softphone::instance()->calls()->setHeld(call, true);
    
    [self notifyAttendedTransferStarted:call];
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(void)cancelAttendedTransfer:(Softphone::EventHistory::CallEvent::Pointer)call
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    if (call.is_null())
        return;
    
    call->transients.erase("markedForAttendedTransfer"_s);
    
    if (Softphone::instance()->calls()->isHeld(call).local == Call::HoldState::Held)
        Softphone::instance()->calls()->setHeld(call, false);
    
    [self notifyAttendedTransferCancelled:call];
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(BOOL)isAttendedTransferring:(Softphone::EventHistory::CallEvent::Pointer)call
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    if (call.is_null())
        return false;
    
    return call->transients["markedForAttendedTransfer"_s] == "1"_s;
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(void)beginForward:(Softphone::EventHistory::CallEvent::Pointer)call
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    if (call.is_null())
        return;
    
    call->transients["markedForFowarding"_s] = "1"_s;
    [self notifyForwardStarted:call];
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(void)cancelForward:(Softphone::EventHistory::CallEvent::Pointer)call
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    if (call.is_null())
        return;
    
    call->transients.erase("markedForFowarding"_s);
    [self notifyForwardCancelled:call];
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(BOOL)isForwarding:(Softphone::EventHistory::CallEvent::Pointer)call
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    if (call.is_null())
        return false;
    
    return call->transients["markedForFowarding"_s] == "1"_s;
}

#pragma mark - Private

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(void)notifyTransferStarted:(Softphone::EventHistory::CallEvent::Pointer)call
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    for (id<TransferChangedDelegate> delegate in _transferChangedDelegates)
    {
        [delegate onTransferStarted:call];
    }
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(void)notifyTransferCancelled:(Softphone::EventHistory::CallEvent::Pointer)call
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    for (id<TransferChangedDelegate> delegate in _transferChangedDelegates)
    {
        [delegate onTransferCancelled:call];
    }
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(void)notifyAttendedTransferStarted:(Softphone::EventHistory::CallEvent::Pointer)call
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    for (id<AttendedTransferChangedDelegate> delegate in _attendedTransferChangedDelegates)
    {
        [delegate onAttendedTransferStarted:call];
    }
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(void)notifyAttendedTransferCancelled:(Softphone::EventHistory::CallEvent::Pointer)call
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    for (id<AttendedTransferChangedDelegate> delegate in _attendedTransferChangedDelegates)
    {
        [delegate onAttendedTransferCancelled:call];
    }
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(void)notifyForwardStarted:(Softphone::EventHistory::CallEvent::Pointer)call
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    for (id<ForwardChangedDelegate> delegate in _forwardChangedDelegates)
    {
        [delegate onForwardStarted:call];
    }
}

//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
-(void)notifyForwardCancelled:(Softphone::EventHistory::CallEvent::Pointer)call
//-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*
{
    for (id<ForwardChangedDelegate> delegate in _forwardChangedDelegates)
    {
        [delegate onForwardCancelled:call];
    }
}

@end
